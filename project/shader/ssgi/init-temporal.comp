#version 460

#extension GL_GOOGLE_include_directive : enable

#include "../common/gbuffer-storage.glsl"
#include "../common/ndc-uv-conv.glsl"
#include "reservoir.glsl"
#include "../common/pbr.glsl"

#extension GL_EXT_debug_printf : enable

//============================================================

layout(local_size_x = 8, local_size_y = 8) in;

layout(set = 0, binding = 0) uniform sampler2D luminance_tex;
layout(set = 0, binding = 1) uniform sampler2D depth_tex;
layout(set = 0, binding = 2) uniform usampler2D light_info_tex;
layout(set = 0, binding = 3) uniform sampler2D blue_noise_tex;
layout(set = 0, binding = 4) uniform sampler2D prev_depth_tex;

layout(set = 0, binding = 5) uniform sampler2D prev_temporal_reservoir_tex1;
layout(set = 0, binding = 6) uniform usampler2D prev_temporal_reservoir_tex2;
layout(set = 0, binding = 7) uniform usampler2D prev_temporal_reservoir_tex3;
layout(set = 0, binding = 8) uniform sampler2D prev_temporal_reservoir_tex4;

layout(set = 1, binding = 0, rgba32f) uniform writeonly image2D temporal_reservoir_tex1;
layout(set = 1, binding = 1, rgba32ui) uniform writeonly uimage2D temporal_reservoir_tex2;
layout(set = 1, binding = 2, rgba32ui) uniform writeonly uimage2D temporal_reservoir_tex3;
layout(set = 1, binding = 3, rgba16f) uniform writeonly image2D temporal_reservoir_tex4;

layout(std140, set = 2, binding = 0) uniform Params
{
    mat4 inv_proj_mat;
    mat4 proj_mat;
    mat4 inv_view_mat;
    mat4 view_mat;
    mat4 back_proj_mat; // prev_camera_view_proj_mat * inv_view, (Current View) -> (World) -> (Prev Clip)

    vec4 inv_proj_mat_col3;
    vec4 inv_proj_mat_col4;

    uvec2 resolution;
    ivec2 time_noise;

    vec2 near_plane_span; // Near plane span in view space, calculated by (right - middle, top - middle)
    float near_plane;
    float max_scene_distance;
    float distance_attenuation;
};

//============================================================

const float FLT_HIGHEST = 1e+38;
const float FLT_LOWEST = -FLT_HIGHEST;
const int MAX_HIZ_LEVEL = 8;
const int MAX_ITER = 96;
const int MAX_SKIP_STEPS = 8;

//============================================================

struct Hit_result
{
    bool hit;
    vec2 uv;
    vec3 V_hit_pos;
};

Hit_result no_hit()
{
    return Hit_result(false, vec2(0.0), vec3(0.0));
}

Hit_result make_hit(vec2 uv, vec3 V_hit_pos)
{
    return Hit_result(true, uv, V_hit_pos);
}

//============================================================

// Calculate the intersection point of ray1 (origin at (0,0,0)) and ray2 (origin at ray2_origin)
vec3 ray_intersection(vec3 ray1_dir, vec3 ray2_origin, vec3 ray2_dir)
{
    const float a = dot(ray1_dir, ray1_dir);
    const float b = dot(ray1_dir, ray2_dir);
    const float c = dot(ray2_dir, ray2_dir);
    const float d = dot(ray1_dir, ray2_origin);
    const float e = dot(ray2_dir, ray2_origin);

    const float t1 = (-c * d + b * e) / (b * b - a * c);
    return ray1_dir * t1;
}

// Normalize a PUV space direction to a unit step in PUV space
vec2 dda_step_direction(vec2 dir)
{
    const vec2 abs_dir = abs(dir.xy);
    return abs_dir.x > abs_dir.y
    ? vec2(sign(dir.x), dir.y / abs_dir.x) : vec2(dir.x / abs_dir.y, sign(dir.y));
}

// Reconstruct view space position from pixel UV coordinate and depth
vec3 P_to_V_pos(vec2 PUV_coord, float depth)
{
    const vec2 uv = PUV_coord / vec2(resolution);
    const vec4 ndc_pos = vec4(uv_to_ndc(uv), depth, 1.0);
    const vec4 V_pos_h = inv_proj_mat * ndc_pos;
    return V_pos_h.xyz / V_pos_h.w;
}

// Transform a world space vector to view space
vec3 W_to_V_vec(vec3 W_vec)
{
    const vec4 W_vec_h = vec4(W_vec, 0.0);
    const vec4 V_vec_h = view_mat * W_vec_h;
    return normalize(V_vec_h.xyz);
}

mat3 build_local_space(vec3 normal)
{
    float sign = normal.z >= 0.0 ? 1.0 : -1.0;
    float a = -1.0 / (sign + normal.z);
    float b = normal.x * normal.y * a;
    vec3 T = vec3(1.0 + sign * normal.x * normal.x * a, sign * b, -sign * normal.x);
    vec3 B = vec3(b, sign + normal.y * normal.y * a, -normal.y);
    return mat3(normalize(T), normalize(B), normal); // T and B are already orthogonal to normal
}

vec3 select_march_direction(vec3 normal,
    vec3 view_dir,
    vec4 noise_unorm)
{
    float u1 = noise_unorm.x;
    float u2 = noise_unorm.y;
    float r = sqrt(u1);
    float phi = 2.0 * PI * u2;
    vec3 local = vec3(r * sin(phi), r * cos(phi), sqrt(max(0.0, 1.0 - u1)));

    mat3 TBN = build_local_space(normal);

    return normalize(TBN * local);
}

// Calculate PUV space march direction from V space march direction
vec2 uv_space_march_dir(vec3 V_march_dir, vec3 V_start)
{
    vec4 clip0 = proj_mat * vec4(V_start, 1.0);
    clip0 /= clip0.w;

    const float base_step = 0.02;
    const float min_step = 0.1;
    float step_len = max(length(V_start) * base_step, min_step);
    vec3 stepped = V_start + V_march_dir * step_len;

    vec4 clip1 = proj_mat * vec4(stepped, 1.0);
    clip1 /= clip1.w;

    vec2 NDC_delta = clip1.xy - clip0.xy;

    vec2 PUV_delta = NDC_delta * vec2(0.5 * float(resolution.x), -0.5 * float(resolution.y));
    return PUV_delta;
}

// Precondition: ray must intersect the rectangle, and d components are non-zero (dda step ensures this)
// rect = vec4(x_min, x_max, y_min, y_max)
vec2 ray_rect_intersect(vec2 P, vec2 d, vec4 rect)
{
    const vec4 ts = (rect - P.xxyy) / d.xxyy;

    vec2 t_enter = min(ts.xz, ts.yw);
    vec2 t_exit = max(ts.xz, ts.yw);

    t_enter.x = isnan(t_enter.x) || isinf(t_enter.x) ? FLT_LOWEST : t_enter.x;
    t_enter.y = isnan(t_enter.y) || isinf(t_enter.y) ? FLT_LOWEST : t_enter.y;
    t_exit.x = isnan(t_exit.x) || isinf(t_exit.x) ? FLT_HIGHEST : t_exit.x;
    t_exit.y = isnan(t_exit.y) || isinf(t_exit.y) ? FLT_HIGHEST : t_exit.y;

    float t_enter_d = max(t_enter.x, t_enter.y);
    float t_exit_d = min(t_exit.x, t_exit.y);

    return vec2(t_enter_d, t_exit_d);
}

vec4 get_hiz_pixelcoord_boundary(int hiz_level, ivec2 hiz_resolution, ivec2 hiz_coord)
{
    int hiz_size = 1 << hiz_level;
    bvec2 coord_at_edge = equal(hiz_coord + 1, hiz_resolution);
    vec2 size = mix(vec2(hiz_size.xx), vec2(hiz_size.xx << 1), vec2(coord_at_edge));
    return vec4(
        vec2(hiz_coord * hiz_size),
        vec2(hiz_coord * hiz_size) + size
    ).xzyw;
}

Hit_result raytrace(vec3 V_start, vec3 V_march_dir, ivec2 PUV_coord)
{
    const vec2 PUV_delta = uv_space_march_dir(V_march_dir, V_start);
    const vec2 PUV_unit_step = dda_step_direction(PUV_delta);

    vec2 PUV_traverse = vec2(PUV_coord) + 0.5;
    const vec2 PUV_initial = PUV_traverse;
    PUV_traverse += PUV_unit_step;

    int hiz_level = 0;
    int hit_penalty = 0;
    int hit_skips = 0;

    const ivec2 resolution_ivec = ivec2(resolution);
    const vec2 resolution_vec = vec2(resolution);

    for (int iter = 0; iter < MAX_ITER; iter++)
    {
        /* Calculate Hi-Z Level Data */

        const int hiz_level_size = 1 << hiz_level;
        const float curr_step = hiz_level_size;
        const ivec2 hiz_level_res = resolution_ivec >> hiz_level;

        /* Fetch Hi-Z Depth */

        const ivec2 UV_hiz_texel_coord = ivec2(floor(PUV_traverse)) >> hiz_level;
        const float P_hiz_depth = texelFetch(depth_tex, UV_hiz_texel_coord, hiz_level).r;

        const vec4 V_hiz_depth = fma(P_hiz_depth.xxxx, inv_proj_mat_col3, inv_proj_mat_col4);
        const float V_hiz_depth_z = -V_hiz_depth.z / V_hiz_depth.w;

        /* Compute Intersection with Hi-Z Pixel Boundary */

        const vec4 hiz_boundary = get_hiz_pixelcoord_boundary(hiz_level, hiz_level_res, UV_hiz_texel_coord);
        const vec2 t_bounds = ray_rect_intersect(PUV_initial, PUV_unit_step, hiz_boundary);

        const float t_enter = t_bounds.x;
        const float t_exit = t_bounds.y;

        const vec2 PUV_traverse_enter = fma(PUV_unit_step, t_bounds.xx, PUV_initial);
        const vec2 PUV_traverse_exit = fma(PUV_unit_step, t_bounds.yy, PUV_initial);

        const vec2 UV_traverse_enter = PUV_traverse_enter / resolution_vec;
        const vec2 UV_traverse_exit = PUV_traverse_exit / resolution_vec;

        const vec3 V_traverse_enter_nearplane = vec3(uv_to_ndc(UV_traverse_enter) * near_plane_span, -near_plane);
        const vec3 V_traverse_exit_nearplane = vec3(uv_to_ndc(UV_traverse_exit) * near_plane_span, -near_plane);

        const vec3 V_traverse_enter = ray_intersection(V_traverse_enter_nearplane, V_start, V_march_dir);
        const vec3 V_traverse_exit = ray_intersection(V_traverse_exit_nearplane, V_start, V_march_dir);

        const float V_traverse_enter_depth = -V_traverse_enter.z;
        const float V_traverse_exit_depth = -V_traverse_exit.z;

        const vec2 UV_traverse = PUV_traverse / resolution_vec;

        /* Precompute conditions */

        const int next_level = min(hiz_level + 1, MAX_HIZ_LEVEL);
        const int prev_level = max(hiz_level - 1, 0);
        const vec2 PUV_next_traverse = fma(t_bounds.yy + 0.05, PUV_unit_step, PUV_initial);

        const bool next_pixcoord_out_of_bounds = any(greaterThanEqual(PUV_next_traverse, resolution_vec))
                || any(lessThan(PUV_next_traverse, vec2(0.0)));

        const bool hiz_empty = P_hiz_depth == 0.0;
        const bool t_outofbounds = any(lessThan(t_bounds, vec2(0.0)));
        const bool hit_detected = V_hiz_depth_z <= max(V_traverse_exit_depth, V_traverse_enter_depth);

        // Empty space, increase HIZ level
        if (hiz_empty)
        {
            if (next_pixcoord_out_of_bounds)
                return no_hit();

            hiz_level = next_level;
            PUV_traverse = PUV_next_traverse;

            continue;
        }

        if (t_outofbounds)
        {
            if (hiz_level == 0) return no_hit();
            hiz_level = prev_level;
            hit_penalty = 2;
            continue;
        }

        if (hit_detected)
        {
            if (hiz_level == 0)
            {
                if (abs(V_traverse_enter_depth - V_hiz_depth_z) / V_traverse_enter_depth < 0.01)
                    return make_hit(UV_traverse, V_traverse_enter);
                else
                    return no_hit();
            }

            hiz_level = prev_level;
            hit_penalty = 4;
            continue;
        }

        if (next_pixcoord_out_of_bounds) return no_hit();

        PUV_traverse = PUV_next_traverse;

        if (hit_penalty > 0)
            hit_penalty--;
        else
            hiz_level = next_level;
    }

    return no_hit();
}

void store_empty_reservoir(ivec2 PUV_coord_half)
{
    imageStore(temporal_reservoir_tex1, PUV_coord_half, vec4(0.0));
    imageStore(temporal_reservoir_tex2, PUV_coord_half, uvec4(0));
    imageStore(temporal_reservoir_tex3, PUV_coord_half, uvec4(0));
    imageStore(temporal_reservoir_tex4, PUV_coord_half, vec4(0.0));
}

//============================================================

ivec2 offsets[9] = ivec2[](
        ivec2(0, 0), ivec2(-1, 0), ivec2(1, 0),
        ivec2(-1, -1), ivec2(0, -1), ivec2(1, -1),
        ivec2(-1, 1), ivec2(0, 1), ivec2(1, 1)
    );

void main()
{
    /* Early Out */

    const ivec2 resolution_ivec = ivec2(resolution);
    const ivec2 PUV_coord_half = ivec2(gl_GlobalInvocationID.xy);
    const ivec2 PUV_coord = PUV_coord_half << 1;

    if (any(greaterThanEqual(PUV_coord, resolution))) return;

    /* Depth Detection */

    const float depth = texelFetch(depth_tex, PUV_coord, 0).r;
    if (depth == 0.0)
    {
        store_empty_reservoir(PUV_coord_half);
        return;
    }

    /* Sample Noise */

    const vec4 noise_unorm = texture(
            blue_noise_tex,
            vec2(PUV_coord_half + time_noise + 0.5) / 128
        );

    /* Fetch GBuffer */

    const uvec2 light_info = texelFetch(light_info_tex, PUV_coord, 0).rg;
    const GBufferLighting lighting = unpack_gbuffer_lighting(light_info);

    const vec3 V_pos = P_to_V_pos(vec2(PUV_coord) + 0.5, depth); // Pixel position at view space
    const vec3 W_start_pos = (inv_view_mat * vec4(V_pos, 1.0)).xyz;
    const vec3 W_start_normal = lighting.normal;
    const vec3 V_normal = W_to_V_vec(lighting.normal); // Pixel Normal at view space
    const vec3 V_eye_to_pixel = normalize(V_pos); // View direction at view space
    const vec3 V_pixel_to_eye = -V_eye_to_pixel;

    /* Fetch Previous Reservoir */

    Reservoir reservoir = Reservoir(vec3(0.0), vec3(0.0), vec3(0.0), vec3(0.0), vec3(0.0), 0.0, 0.0, 0.0);

    const vec4 P_prev_pos_h = back_proj_mat * vec4(V_pos, 1.0);
    const vec3 P_prev_ndc = P_prev_pos_h.xyz / P_prev_pos_h.w;
    const vec2 UV_prev = ndc_to_uv(P_prev_ndc.xy);
    const vec2 UV_pixel_unit = vec2(1.0) / vec2(resolution >> 1);

    if (all(lessThanEqual(abs(P_prev_ndc.xy), vec2(1.0))) && P_prev_pos_h.w > 0.0)
    {
        float prev_depth = textureLod(prev_depth_tex, UV_prev, 0).r;

        if (distance(P_prev_ndc.z, prev_depth) / P_prev_ndc.z < 0.00001)
        {
            uvec4 candidate;
            int candidate_index;
            float min_distance = FLT_HIGHEST;

            for (int i = 0; i < 9; i++)
            {
                vec2 UV_neighbor = UV_prev + vec2(offsets[i]) * UV_pixel_unit;
                uvec4 neighbor_tex3 = textureLod(prev_temporal_reservoir_tex3, UV_neighbor, 0);
                vec3 neighbor_start_pos = vec3(
                        uintBitsToFloat(neighbor_tex3.x),
                        uintBitsToFloat(neighbor_tex3.y),
                        uintBitsToFloat(neighbor_tex3.z)
                    );

                float distance = length(neighbor_start_pos - W_start_pos);
                if (distance < min_distance)
                {
                    min_distance = distance;
                    candidate = neighbor_tex3;
                    candidate_index = i;
                }
            }

            const vec2 UV_candidate = UV_prev + vec2(offsets[candidate_index]) * UV_pixel_unit;

            reservoir = decode_reservoir(
                    textureLod(prev_temporal_reservoir_tex1, UV_candidate, 0),
                    textureLod(prev_temporal_reservoir_tex2, UV_candidate, 0),
                    candidate,
                    textureLod(prev_temporal_reservoir_tex4, UV_candidate, 0)
                );
        }
    }

    /* Ray Marching */

    vec3 V_march_dir = select_march_direction(
            V_normal,
            V_eye_to_pixel,
            noise_unorm
        );

    const Hit_result trace_result = raytrace(V_pos, normalize(V_march_dir), PUV_coord);

    if (trace_result.hit)
    {
        const float p = 1.0;

        const vec3 W_hit_pos = (inv_view_mat * vec4(trace_result.V_hit_pos, 1.0)).xyz;
        const vec3 W_hit_normal = octToNormal(unpackSnorm2x16(textureLod(light_info_tex, trace_result.uv, 0.0).r));
        const vec3 V_hit_normal = W_to_V_vec(W_hit_normal);

        const vec4 luminance = textureLod(luminance_tex, trace_result.uv, 0) * dot(V_hit_normal, -V_march_dir);

        const float brightness = dot(luminance.rgb, vec3(0.2126, 0.7152, 0.0722)) + 0.01;
        const float pdf = gltf_pbr_pdf(
                normalize(trace_result.V_hit_pos - V_pos),
                V_pixel_to_eye,
                lighting.roughness,
                V_normal
            );
        const float p_hat = brightness * mix(0.1, 1.0, max(0.0, pdf));

        update_reservoir(
            reservoir,
            p_hat,
            p,
            W_hit_pos,
            W_hit_normal,
            W_start_pos,
            W_start_normal,
            luminance.rgb,
            noise_unorm
        );
    }
    else
    {
        update_reservoir(
            reservoir,
            0.0,
            1.0,
            vec3(0.0),
            vec3(0.0),
            W_start_pos,
            W_start_normal,
            vec3(0.0),
            noise_unorm
        );
    }

    clamp_reservoir(reservoir, 40);

    vec4 tex1_out;
    uvec4 tex2_out;
    uvec4 tex3_out;
    vec4 tex4_out;

    encode_reservoir(reservoir, tex1_out, tex2_out, tex3_out, tex4_out);

    imageStore(temporal_reservoir_tex1, PUV_coord_half, tex1_out);
    imageStore(temporal_reservoir_tex2, PUV_coord_half, tex2_out);
    imageStore(temporal_reservoir_tex3, PUV_coord_half, tex3_out);
    imageStore(temporal_reservoir_tex4, PUV_coord_half, tex4_out);
}
