#version 460

#extension GL_GOOGLE_include_directive : enable
#include "../common/gbuffer-storage.glsl"
#include "../common/ndc-uv-conv.glsl"

#extension GL_EXT_debug_printf : enable

//============================================================

layout(local_size_x = 8, local_size_y = 8) in;

layout(set = 0, binding = 0) uniform sampler2D luminance_tex;
layout(set = 0, binding = 1) uniform sampler2D depth_tex;
layout(set = 0, binding = 2) uniform usampler2D light_info_tex;
layout(set = 0, binding = 3) uniform sampler2D blue_noise_tex;
layout(set = 0, binding = 4) uniform sampler2D albedo_tex;

layout(set = 1, binding = 0) uniform writeonly image2D ssgi_trace_out;

layout(std140, set = 2, binding = 0) uniform Params
{
    mat4 inv_proj_mat;
    mat4 proj_mat;
    mat4 view_mat;

    vec4 inv_proj_mat_col3;
    vec4 inv_proj_mat_col4;

    uvec2 resolution;
    ivec2 time_noise;

    vec2 near_plane_span; // Near plane span in view space, calculated by (right - middle, top - middle)
    float near_plane;
    float max_scene_distance;
};

//============================================================

struct Ray
{
    vec3 origin;
    vec3 direction;
};

vec3 ray_intersection(vec3 ray1_dir, Ray ray2)
{
    const float a = dot(ray1_dir, ray1_dir);
    const float b = dot(ray1_dir, ray2.direction);
    const float c = dot(ray2.direction, ray2.direction);
    const float d = dot(ray1_dir, ray2.origin);
    const float e = dot(ray2.direction, ray2.origin);

    const float t1 = (-c * d + b * e) / (b * b - a * c);
    return ray1_dir * t1;
}

vec2 dda_step_direction(vec2 dir)
{
    const vec2 abs_dir = abs(dir.xy);
    return abs_dir.x > abs_dir.y
    ? vec2(sign(dir.x), dir.y / abs_dir.x) : vec2(dir.x / abs_dir.y, sign(dir.y));
}

vec3 reconstruct_view_pos_reverse_z(vec2 pixel_coord, float depth)
{
    const vec2 uv = pixel_coord / vec2(resolution);
    const vec4 ndc_pos = vec4(uv_to_ndc(uv), depth, 1.0);
    const vec4 view_pos_homo = inv_proj_mat * ndc_pos;
    return view_pos_homo.xyz / view_pos_homo.w;
}

vec3 reconstruct_view_normal(vec3 world_normal)
{
    const vec4 world_normal_h = vec4(world_normal, 0.0);
    const vec4 view_normal_h = view_mat * world_normal_h;
    return view_normal_h.xyz;
}

vec3 select_march_direction(vec3 view_normal, vec3 view_dir, vec4 noise_unorm, float metalness, float roughness)
{
    // Build local frame
    vec3 N = normalize(view_normal);
    vec3 T = cross(abs(N.x) < 0.999 ? vec3(1.0, 0.0, 0.0) : vec3(0.0, 0.0, 1.0), N);
    vec3 B = cross(N, T);
    
    float theta = noise_unorm.x * (3.1415926 * 2);
    float r = sqrt(noise_unorm.y);
    vec2 xy = vec2(r * cos(theta), r * sin(theta));
    float z = sqrt(1 - dot(xy, xy));
    return N * z + T * xy.x + B * xy.y;
}

vec2 uv_space_march_dir(vec3 march_dir, vec3 view_pos)
{
    vec4 clip0 = proj_mat * vec4(view_pos, 1.0);
    clip0 /= clip0.w;

    const float base_step = 0.02;
    const float min_step = 0.1;
    float step_len = max(length(view_pos) * base_step, min_step);
    vec3 stepped = view_pos + march_dir * step_len;

    vec4 clip1 = proj_mat * vec4(stepped, 1.0);
    clip1 /= clip1.w;

    vec2 ndc_delta = clip1.xy - clip0.xy;

    vec2 pixel_delta = ndc_delta * vec2(0.5 * float(resolution.x), -0.5 * float(resolution.y));
    return pixel_delta;
}

const float FLT_HIGHEST = 1e+38;
const float FLT_LOWEST = -FLT_HIGHEST;

// Precondition: ray must intersect the rectangle, and d components are non-zero (dda step ensures this)
vec2 rayRectIntersection(vec2 P, vec2 d, vec4 rect) {
    // rect = vec4(x_min, x_max, y_min, y_max)
    const vec4 ts = (rect - P.xxyy) / d.xxyy;

    vec2 t_enter = min(ts.xz, ts.yw);
    vec2 t_exit = max(ts.xz, ts.yw);

    t_enter.x = isnan(t_enter.x) || isinf(t_enter.x) ? FLT_LOWEST : t_enter.x;
    t_enter.y = isnan(t_enter.y) || isinf(t_enter.y) ? FLT_LOWEST : t_enter.y;
    t_exit.x = isnan(t_exit.x) || isinf(t_exit.x) ? FLT_HIGHEST : t_exit.x;
    t_exit.y = isnan(t_exit.y) || isinf(t_exit.y) ? FLT_HIGHEST : t_exit.y;

    float t_enter_d = max(t_enter.x, t_enter.y);
    float t_exit_d = min(t_exit.x, t_exit.y);

    return vec2(t_enter_d, t_exit_d);
}

vec4 get_hiz_pixelcoord_boundary(int hiz_level, ivec2 hiz_resolution, ivec2 hiz_coord)
{
    int hiz_size = 1 << hiz_level;
    bvec2 coord_at_edge = equal(hiz_coord + 1, hiz_resolution);
    vec2 size = mix(vec2(hiz_size), vec2(hiz_size) * 2, vec2(coord_at_edge));
    return vec4(
        vec2(hiz_coord * hiz_size),
        vec2(hiz_coord * hiz_size) + size
    ).xzyw;
}

const int MAX_HIZ_LEVEL = 8;
const int MAX_ITER = 96;

int iter;

// retuning `a` component as hit/miss flag (1.0 = hit, 0.0 = miss)
vec4 raytrace(vec3 view_pos, vec3 march_dir)
{
    const Ray march_ray = Ray(
            view_pos,
            march_dir
        );

    const vec2 pixel_delta = uv_space_march_dir(march_dir, view_pos);
    const vec2 dda_step = dda_step_direction(pixel_delta);

    vec2 traverse_screen_pixcoord = vec2(gl_GlobalInvocationID.xy) + 0.5;
    const vec2 initial_screen_pixcoord = traverse_screen_pixcoord;
    traverse_screen_pixcoord += dda_step; // Advance one step to avoid self-intersection

    int hiz_level = 0;
    int hit_penalty = 0;

    const ivec2 resolution_ivec = ivec2(resolution);
    const vec2 resolution_vec = vec2(resolution);

    for (iter = 0; iter < MAX_ITER; iter++)
    {
        /* Calculate Hi-Z Level Data */

        const int hiz_level_size = 1 << hiz_level;
        const float curr_step = hiz_level_size;
        const ivec2 hiz_level_res = resolution_ivec >> hiz_level;

        /* Fetch Hi-Z Depth */

        const ivec2 hiz_texel_coord = ivec2(floor(traverse_screen_pixcoord)) >> hiz_level;
        const float hiz_depth = texelFetch(depth_tex, hiz_texel_coord, hiz_level).r;

        const vec4 hiz_depth_clip = fma(hiz_depth.xxxx, inv_proj_mat_col3, inv_proj_mat_col4);
        const float hiz_depth_view_z = -hiz_depth_clip.z / hiz_depth_clip.w;

        /* Compute Intersection with Hi-Z Pixel Boundary */

        const vec4 hiz_boundary = get_hiz_pixelcoord_boundary(hiz_level, hiz_level_res, hiz_texel_coord);
        const vec2 t_bounds = rayRectIntersection(initial_screen_pixcoord, dda_step, hiz_boundary);

        const float t_enter = t_bounds.x;
        const float t_exit = t_bounds.y;

        const vec2 traverse_screen_enter_pixcoord = fma(dda_step, t_bounds.xx, initial_screen_pixcoord);
        const vec2 traverse_screen_exit_pixcoord = fma(dda_step, t_bounds.yy, initial_screen_pixcoord);

        const vec2 traverse_screen_enter_uv = traverse_screen_enter_pixcoord / resolution_vec;
        const vec2 traverse_screen_exit_uv = traverse_screen_exit_pixcoord / resolution_vec;

        const vec3 traverse_screen_enter_view_pos = vec3(uv_to_ndc(traverse_screen_enter_uv) * near_plane_span, -near_plane);
        const vec3 traverse_screen_exit_view_pos = vec3(uv_to_ndc(traverse_screen_exit_uv) * near_plane_span, -near_plane);

        const vec3 traverse_enter_view_pos = ray_intersection(traverse_screen_enter_view_pos, march_ray);
        const vec3 traverse_exit_view_pos = ray_intersection(traverse_screen_exit_view_pos, march_ray);

        const float traverse_enter_view_z = -traverse_enter_view_pos.z;
        const float traverse_exit_view_z = -traverse_exit_view_pos.z;

        const vec2 traverse_uv = traverse_screen_pixcoord / resolution_vec;

        /* Precompute conditions */

        const int next_level = min(hiz_level + 1, MAX_HIZ_LEVEL);
        const int prev_level = max(hiz_level - 1, 0);
        const vec2 next_pixcoord = fma(t_bounds.yy + 0.05, dda_step, initial_screen_pixcoord);

        const bool next_pixcoord_out_of_bounds = any(greaterThanEqual(next_pixcoord, resolution_vec))
                || any(lessThan(next_pixcoord, vec2(0.0)));

        // Empty space, increase HIZ level
        if (hiz_depth == 0.0)
        {
            if (next_pixcoord_out_of_bounds)
                return vec4(0.0);

            hiz_level = next_level;
            traverse_screen_pixcoord = next_pixcoord;

            continue;
        }

        if (any(lessThan(t_bounds, vec2(0.0))))
        {
            if (hiz_level == 0) return vec4(0.0);
            hiz_level = prev_level;
            continue;
        }

        // Hit detected
        if (hiz_depth_view_z <= max(traverse_exit_view_z, traverse_enter_view_z))
        {
            if (hiz_level == 0)
            {
                if (abs(traverse_enter_view_z - hiz_depth_view_z) / traverse_enter_view_z < 0.01)
                {
                    const vec2 ndc = uv_to_ndc(traverse_screen_enter_uv);
                    const float mult = smoothstep(1.0, 0.97, max(abs(ndc.x), abs(ndc.y)));
                    return vec4(textureLod(luminance_tex, traverse_screen_exit_uv, 0).rgb, mult);
                }
                else
                    return vec4(0.0);
            }

            hiz_level = prev_level;
            hit_penalty = 4;
            continue;
        }

        if (next_pixcoord_out_of_bounds) return vec4(0.0);

        traverse_screen_pixcoord = next_pixcoord;
        if (hit_penalty > 0)
            hit_penalty --;
        else
            hiz_level = next_level;
    }

    return vec4(0.0);
}

shared bool early_exit;

void main()
{
    const ivec2 resolution_ivec = ivec2(resolution);
    const ivec2 pixel_coord = ivec2(gl_GlobalInvocationID.xy);
    
    if(gl_LocalInvocationIndex == 0)
    {
        const ivec2 pixel_coord_mip3 = pixel_coord >> 3;
        early_exit = texelFetch(depth_tex, pixel_coord_mip3, 3).r == 0.0;
    }

    barrier();

    if(early_exit || any(greaterThanEqual(gl_GlobalInvocationID.xy, resolution)))
    {
        imageStore(ssgi_trace_out, pixel_coord, vec4(0.0));
        return;
    }

    /* Texture Fetch */

    const float depth = texelFetch(depth_tex, pixel_coord, 0).r;

    if (depth == 0.0)
    {
        imageStore(ssgi_trace_out, pixel_coord, vec4(0.0));
        return;
    }

    const vec4 noise_unorm = texture(
            blue_noise_tex,
            vec2(pixel_coord + time_noise) / 128
        );
    const uvec2 light_info = texelFetch(light_info_tex, pixel_coord, 0).rg;

    /* Reconstruct Data */

    const GBufferLighting lighting = unpack_gbuffer_lighting(light_info);

    /* Direction Calculation */

    const vec3 view_pos = reconstruct_view_pos_reverse_z(vec2(pixel_coord) + 0.5, depth); // Pixel position at view space
    const vec3 view_normal = reconstruct_view_normal(lighting.normal); // Pixel Normal at view space
    const vec3 view_dir = normalize(view_pos); // View direction from camera to pixel

    const vec3 march_dir = normalize(select_march_direction(
                view_normal,
                view_dir,
                noise_unorm,
                lighting.metalness,
                lighting.roughness
            ));

    const vec4 trace_result = raytrace(view_pos, march_dir);

    const vec3 albedo = texture(albedo_tex, (vec2(pixel_coord) + 0.5) / vec2(resolution_ivec)).rgb;

    imageStore(ssgi_trace_out, pixel_coord, vec4(trace_result.rgb * trace_result.a * albedo, trace_result.a));
}
