#version 460

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform sampler2D prev_upsample_mip;
layout(set = 0, binding = 1) uniform sampler2D cur_downsample_mip;
layout(set = 1, binding = 0, rgba16f) uniform writeonly image2D cur_upsample_tex;

layout(std140, set = 2, binding = 0) uniform Param
{
    float attenuation;
};

void main()
{
    ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 image_size = imageSize(cur_upsample_tex);

    vec2 pix_size = 1.0 / vec2(image_size);
    vec2 uv = (vec2(coord) + 0.5) * pix_size;

    if (any(greaterThanEqual(coord, image_size)))
        return;

    vec4 sum = textureLod(prev_upsample_mip, uv + pix_size * vec2(1, 1), 0);
    sum += textureLod(prev_upsample_mip, uv + pix_size * vec2(-1, 1), 0);
    sum += textureLod(prev_upsample_mip, uv + pix_size * vec2(1, -1), 0);
    sum += textureLod(prev_upsample_mip, uv + pix_size * vec2(-1, -1), 0);
    sum *= 0.25 * attenuation;

    sum += textureLod(cur_downsample_mip, uv, 0);

    imageStore(cur_upsample_tex, coord, sum);
}
