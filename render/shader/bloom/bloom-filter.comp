#version 460

#extension GL_EXT_samplerless_texture_functions : enable

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform texture2D luminance_in; // Luminance texture input
layout(set = 0, binding = 1) readonly buffer Exposure // Exposure parameters
{
    float avg_luminance;
    float exposure_mult;
};

layout(set = 1, binding = 0, rgba16f) uniform writeonly image2D filtered_out; // Filtered output texture

layout(set = 2, binding = 0) uniform Params
{
    float start_threshold;
    float end_threshold;
};

const float brightness_clamp = 100;

void main()
{
    if (any(greaterThanEqual(gl_GlobalInvocationID.xy, imageSize(filtered_out))))
        return;

    vec3 luminance = texelFetch(luminance_in, ivec2(gl_GlobalInvocationID.xy), 0).rgb * exposure_mult;

    const float brightness = dot(luminance, vec3(0.2126, 0.7152, 0.0722));
    const float mult = smoothstep(
            start_threshold,
            end_threshold,
            brightness
        );
    const float normalized_brightness = brightness * mult;
    const float brightness_clamped = min(normalized_brightness, end_threshold * 3);
    vec3 filtered_value = luminance * mult * (brightness_clamped / max(normalized_brightness, 0.0001));

    if (any(isnan(filtered_value)) || any(isinf(filtered_value)))
        filtered_value = vec3(0.0);

    imageStore(filtered_out, ivec2(gl_GlobalInvocationID.xy), vec4(filtered_value, 0.0));
}
